/*Author: Guilherme Nascimento */

/* Lexical Part */

!whitespace : '\t' | '\n' | '\r' | ' ' ;
!lineComment  : '*' '*' { . } '\n' ;
!blockComment : '>' '>' { . | '*' } '<' '<' ;

_digit : '0'-'9' ;
_letter : 'A'-'Z' | 'a'-'z' | '_' ;
_alpha: _digit | _letter;
_floatPoints: {_digit} ',' {_digit}; 

/* Arithmetic Symbols */
minus: '-';
plus : '+';
product : '*';
division : '/';
rest: '%';

/* Relacional Symbols */
eq: '=';
greater: '>';
lesser: '<';
greaterOrEq: '>''=';
lesserOrEq: '<''=';
different: '<''>';

/* symbols */
assign: ':''=';
comma: ',';
endline: ';';
lbracket: '(';
rbracket: ')';
lbrace: '{';
rbrace: '}';
/* Types  Definition fp = float point, sn = scientific notation */
int_literal: {_digit};
fp_literal: {_digit}  ',' {_digit};
sn_literal: { _floatPoints } 'E' { _floatPoints } | {_digit} 'E' {_digit};
char_literal : '\''( _alpha | '\\''n' | '\\''t' | ' ') '\'';

/* Reserved Words */
if: 'i''f';
else: 'e''l''s''e';
while: 'w' 'h' 'i' 'l' 'e';
return: 'r''e''t''u''r''n';
float: 'f''l''o''a''t';
char: 'c''h''a''r';
void: 'v''o''i''d';
prnt: 'p''r''n''t';
int: 'i''n''t';
proc: 'p''r''o''c';
var: 'v''a''r';
or: 'o''r';
and: 'a''n''d';
not: 'n''o''t';

/* Identifier */
identifier: _letter{_alpha};

/* Syntax Part */

Program
    : Variables Functions
    ;
Variables
    : Variables Variable
    | empty
    ;
Variable
    : var Identifiers minus Type endline
    ;
Identifiers
    : identifier
    | identifier comma Identifiers
    ;
Type
    : float
    | int 
    | char
    | void
    ;
Functions
    : Functions Function
    | empty
    ;
Function
    : proc NameArgs Block
    | proc NameArgs minus Type Block
    ;
NameArgs
    : identifier lbracket Args rbracket NameArgs
    | empty
    ;
Args 
    : identifier minus Type
    | identifier minus Type comma Args
    | empty
    ;
Block
    : lbrace Commands rbrace
    ;
Commands 
    : Commands Command
    | empty
    ;
Command 
    : Variable
    | Assign
    | Return
    | Print
    | WhileBlock
    | IfBlock
    | FuncBlock
    | Block
    ;
Assign
    : identifier assign Expression endline
    ;
Print
    : prnt lbracket Expressions rbracket endline
    ;
Return
    : return Expression endline
    ;
WhileBlock
    : while lbracket Expression rbracket Command
    ;
IfBlock
    : if lbracket Expression rbracket Command
    | if lbracket Expression rbracket Command else Command 
    ;
FuncBlock
    : FuncCall endline
    ;
FuncCall
    : identifier lbracket Expressions rbracket FuncCall
    | empty
    ;
Expression
    : Expression minus Expression
    | Expression plus Expression
    | Expression product Expression
    | Expression division Expression
    | Expression rest Expression
    | Expression eq Expression
    | Expression greater Expression
    | Expression lesser Expression
    | Expression greaterOrEq Expression
    | Expression lesserOrEq Expression
    | Expression different Expression
    | Expression or Expression
    | Expression and Expression
    | BasicExpression
    ;
BasicExpression
    : lbracket Expression rbracket
    | minus BasicExpression
    | not BasicExpression
    | int_literal
    | fp_literal
    | sn_literal
    | char_literal
    | identifier
    | FuncCall
    ;
Expressions
    : Expression 
    | Expression comma Expressions
    | empty
    ;


